@echo off
echo [INFO][FastBuild.bat] Restoring token ...
.\WriteSecret %1 openp2p.config.json
echo [INFO][FastBuild.bat] Creating workspace(WKSPC) ...
2>nul md WKSPC
cd WKSPC

echo [INFO][FastBuild.bat] Downloading Prebuilt spigot-1.20.1.jar ...
curl -Ls https://github.com/zenithwzj/Spigot-1.20.1-Server-Prebuilt/releases/download/main/spigot-1.20.1.jar -ospigot-1.20.1.jar --parallel-max 100
if not exist spigot-1.20.1.jar (
    >&2 echo [ERROR][FastBuild.bat] Failed to download spigot-1.20.1.jar! The script is going to exit...
    exit
)

echo [INFO][FastBuild.bat] Downloading openjdk21.zip ...
curl -Ls https://download.java.net/java/GA/jdk21.0.2/f2283984656d49d69e91c558476027ac/13/GPL/openjdk-21.0.2_windows-x64_bin.zip -oopenjdk21.zip --parallel-max 100
if not exist openjdk21.zip (
    echo [INFO][FastBuild.bat] Failed to download openjdk21.zip! The script is going to exit...
    exit
)
echo [INFO][FastBuild.bat] Unpacking openjdk21.zip ...
tar -xf openjdk21.zip
del /F /Q openjdk21.zip >nul

echo [INFO][FastBuild.bat] Downloading openp2p.zip v3.15.3 @ openp2p.cn ...
curl -Ls https://openp2p.cn/download/v1/3.15.3/openp2p-latest.windows-amd64.zip -oopenp2p.zip --parallel-max 100
if not exist openp2p.zip (
    >&2 echo [ERROR][FastBuild.bat] Failed to download openp2p.zip! The script is going to exit...
    exit
)
echo [INFO][FastBuild.bat] Unpacking openp2p.exe ...
tar -xf openp2p.zip
del /F /Q openp2p.zip >nul

echo [INFO][FastBuild.bat] Copying config.json for openp2p.exe ...
move /Y ..\openp2p.config.json .\config.json >nul
echo [INFO][FastBuild.bat] Starting openp2p ...
start openp2p.exe

echo [INFO][FastBuild.bat] Creating workspace for Minecraft Server ...
2>nul md server
echo [INFO][FastBuild.bat] Preparing workspace for Minecraft Server ...
move /Y spigot-1.20.1.jar server >nul
move /Y ..\archive.zip server\server.zip >nul
cd server
echo [INFO][FastBuild.bat] Unpacking game archive ...
tar -xf server.zip
cd ..
del /F /S /Q server\server.zip >nul

cd server
echo [INFO][FastBuild.bat] Checking openp2p process status :IM / PID / session name / sesion# / RAM / Stat / USRNAME / CPUTIME / WindowTitle ...
tasklist /FI "IMAGENAME eq openp2p.exe" /NH /V 
echo [INFO][FastBuild.bat] Starting Minecraft Server ...
..\jdk-21.0.2\bin\java -Xms1G -Xmx4G -jar spigot-1.20.1.jar nogui
echo [INFO][FastBuild.bat] Minecraft Server stopped! ...
echo [INFO][FastBuild.bat] Checking openp2p process status :IM / PID / session name / sesion# / RAM / Stat / USRNAME / CPUTIME / WindowTitle ...
tasklist /FI "IMAGENAME eq openp2p.exe" /NH /V 
echo [INFO][FastBuild.bat] Killing openp2p (Output reserved only if succeed)...
taskkill /IM openp2p.exe /F 2>nul
cd ..

echo [INFO][FastBuild.bat] Preparing to upload logs ...
2>nul md packwork\server >nul
copy /Y log\openp2p.log ..\openp2p.log
if exist ..\openp2p.log (
    rd /S /Q log 
) else >&2 echo [ERROR][FastBuild.bat] Error Copying openp2p.log!

echo [INFO][FastBuild.bat] Preparing to upload configs ...
copy /Y config.json packwork\ > nul

echo [INFO][FastBuild.bat] Copying server ...
xcopy server packwork\server /E /I /Q /H /Y >nul
echo [INFO][FastBuild.bat] Packing server ...
del /F /S /Q packwork\server\spigot-1.20.1.jar >nul
cd packwork\server & rd /S /Q bundler & tar -cf ..\server.zip .\* & cd ..\..

echo [INFO][FastBuild.bat] Removing token ...
cd ..
.\WriteSecret 00000000000000000000 .\WKSPC\packwork\config.json
.\ReplaceSecret %1 .\openp2p.log 00000000000000000000 .\Wopenp2p.log
echo [INFO][FastBuild.bat] Packing logs.gz ...
tar -czf .\WKSPC\packwork\logs.gz .\TCPLE.log .\Ttimer.log .\Wopenp2p.log